// WebOutHkGen() shows Hk Parameters via a Weboutput()
//
// input:
// - HkName:      Name of the Hk as shown in the WebOutput()
// - ID:          the WebOutput(ID)[]
// - GA_Vl        GA of Vorlauf Temp
// - GA_Rl        Ga of Ruecklauf Temp
// - GA_HkModeReq Requested Hk Mode in UVR1611 u16
// - Ga_HkModeAct Actuel Hk Mode in UVR1611 u16
// - GA_PID       Burner PID in 16u
// - GA_bPump     Pump Status (running?) b1
// - GA_wbFrost    Warning: Frost b1
// - GA_bGenHzON   Heatingting system general status ON/OFF
//
//
:begin SpWebOutHkGen( HkName,Id, GabGenHzON,GaVl,GaRl,GabPump,GaPID,GaHkModeReq,GaHkModeAct,GaOverTempC,GaOverTempW,GabFrost )
//
// HTML style color strings
//
:var strColorBase@
strColorBase@ = $black$

:var strColorUnused@
strColorUnused@ = $grey$

:var strColorNormal@
strColorNormal@ = $green$

:var strColorCaution@ 
strColorCaution@ = $DarkOrange$

:var strColorWarning@
strColorWarning@ = $red$
//
// HTML text
//
:var strND@
strND@ = $ND$

:var strON@
strON@   = $ON$

:var strOFF@
strOFF@  = $OFF$

:var strTitle@
strTitle@ = HkName

:var html@
html@ = $?$c16000

:var style1@
style1@ = $?$c16000
//
// HTML variables
//
:var strColorHkStatus@
strColorHkStatus@ = strColorUnused@
:var strHkStatus@
strHkStatus@ = strND@

:var strTemp@
strTemp@ = $nil$
:var strColorVLRL@
strColorVLRL@ = strColorUnused@
:var fMaxTempDiff@
fMaxTempDiff@ = 5f16

:var strColorPump@
strColorPump@ = strColorUnused@
:var strPump@
strPump@       = strND@

:var strColorPID@
strColorPID@ = strColorUnused@
:var strPID@ 
strPID@ = strND@

:var strColorHkMode@
strColorHkMode@   = strColorUnused@
:var strHkMode@
strHkMode@        = strND@

:var strOverT@
strOverT@         = $$
:var strCOverTC@
strCOverTC@        = $<span style='color:$+ strColorCaution@ +$'> OverTemp</span> $  

:var strCOverTW@
strCOverTW@        = $<span style='color:$+ strColorWarning@ +$'> OverTemp</span> $  

:var strFrost@
strFrost@ = $$
:var strCFrost@  
strCFrost@        = $<span style='color:$+ strColorWarning@ +$'> Frost</span> $  
//
/////////////////////////////////////////////////////////////////////////////
///
:var STARTDELAY@
STARTDELAY@    = 2000u64

:var iDelay@
iDelay@ = 1
if( after(systemstart(), STARTDELAY@) or cycle(iDelay@,0 ))then{

   if GabGenHzON then{
		strColorHkStatus@ = strColorNormal@;
		strHkStatus@      = strON@;      
		}endif ;
	if(!GabGenHzON) then{
	    strColorHkStatus@ = strColorUnused@;
	    strHkStatus@      = strOFF@;
	    }endif;

   strTemp@ =  stringformat(convert( GaVl,0f16 ) ,4,3,2,0 ) + $C/$ +    \\
               stringformat(convert( GaRl,0f16 ) ,4,3,2,0 ) + $C$ ;

   strColorVLRL@ = strColorNormal@;
   if( convert(GaVl,0f16) - convert( GaRl,0f16 ) >  fMaxTempDiff@ ) then{
      strColorVLRL@ = strColorCaution@;
      }endif;
 
   if GabPump then{
      strPump@       = strON@;
      strColorPump@  = strColorNormal@;
      }endif ;
   if !GabPump then{
      strPump@       = strOFF@;
      strColorPump@  = strColorUnused@;cd 
      }endif ;

   strColorPID@ =  strColorUnused@ ;
   if (convert(GaPID, 0u16) > 20u16) then{ strColorPID@ = strColorNormal@ }endif;
   strPID@ =   stringformat(convert(GaPID,0u16),0,3,3,3 )+$%$; 

   strColorHkMode@ = strColorNormal@;
//   if( GaHkModeReq != GaHkModeAct) then{ strColorHkMode@ = strColorCaution@} endif;
   strHkMode@ = strND@;
   if( convert(GaHkModeAct,0u16) == UVR_NORMAL )      then{ strHkMode@ = $high$} endif;
   if( convert(GaHkModeAct,0u16) == UVR_ABGESENKT )   then{ strHkMode@ = $low$} endif;
   if( convert(GaHkModeAct,0u16) == UVR_AUTO)         then{ strHkMode@ = $auto$} endif;
   if( convert(GaHkModeAct,0u16) == UVR_SBY)          then{ strHkMode@ = $frost$} endif; 
   if( convert(GaHkModeAct,0u16) == UVR_INTERN )      then{ strHkMode@ = $intern$} endif;
   if( convert(GaHkModeAct,0u16) == UVR_ERROR )       then{strHkMode@ = $error$;strColorHkMode@ = strColorWarning@;} endif;

   strOverT@ = $$;
   if(GaOverTempC) then{strOverT@ = strCOverTC@} endif;
   if(GaOverTempW) then{strOverT@ = strCOverTW@} endif;

   strFrost@ = $$;
   if( GabFrost ) then{strFrost@ = strCFrost@}endif;
      
style1@ =  $<style  >                                                 
    .head   { margin:0 0 0 0; font-size: 15px;background-color: lightgrey; }                                      
    .line   { display:flex; width:100%; align-content: space-between; font-weight:bold;font-size: 12px;}
    .col1   { flex: 0 0 7em;  background-color: lightgrey;}                                  
    .col2   { flex: 0 0 9em; background-color: lightgrey;}                                  
    .col3   { flex: 0 0 6em;  background-color: lightgrey;}                                   
    .col4   { flex: 0 0 7em;  background-color: lightgrey;   }                               
    .col5   { flex: 0 0 7em;  background-color: lightgrey;   }   
     </style>     
    $;                                    
                                          
html@ = $<h2 class= 'head'  >$+ HkName +$</h2>                
    <div  class= line >  	                             
        <div class= col1 >Hk: <span style='color:$+ strColorHkStatus@ +$'>
        $+ strHkStatus@ +$</span></div>                
        <div class =  col2 >VL/RL: <span style = 'color:$+ strColorVLRL@ +$' >
        $+ strTemp@ +$</span></div>                                
        <div class =  col3  >Pump: <span style = 'color:$+ strColorPump@ +$' >
        $+ strPump@ +$</span> </div>                            
        <div class =  col4 >PID: <span style ='color:$+ strColorPID@ +$'>
         $+  strPID@ +$</span></div>                            
    </div>                                                    
    <div class =  line >                                      
        <div class=  col1 >Mode: <span style ='color:$+ strColorHkMode@ +$'>
        $+ strHkMode@ +$</span> </div>                            
        <div class = col2>$+ strOverT@    +$</div>                            
        <div class =  col3  >$+ strFrost@   +$</div>                            
        <div   class =  col4 >       </div>                            
    </div>                                                    
    $;
 
   weboutput( Id,style1@+html@ );
 }endif
:end
