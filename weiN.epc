
[ETS-ESF]
// ESF file exported from ETS3
D:/users/asc/repo/EibPC/EtsExport/Weisshauptstr.esf

[Macros]
//Sp_Hk5OG1control("OG1MaxStellwertOut-3/3/105", 95%,Hk5_SwOg1)
// SpWebOutHkGen( HkName,Id, GabGenHzON,GaVl,GaRl,GabPump,GaPID,GaHkModeReq,GaHkModeAct,GaOverTempC,GaOverTempW,GabFrost )
SpWebOutHkGen($EG FBH$,ID_WebOutHK1,"Din14-11/0/14","Ain02-11/2/2","Ain03-11/2/3","Din04-11/0/4","Ain04-11/2/4","Aout04-11/3/4","Ain05-11/2/5","Din010-11/0/10","Din09-11/0/9","Din01-11/0/1",0)
SpWebOutHkGen($RAD$,ID_WebOutHK3,"Din14-11/0/14","Ain10-11/2/10","Ain11-11/2/11","Din05-11/0/5",0,"Aout06-11/3/6","Ain15-11/2/15","Din08-11/0/8","Din07-11/0/7","Din03-11/0/3","OG1MaxStellwertOut-3/3/105")
SpWebOutHkGen($DACH FBH$,ID_WebOutHK4,"Din14-11/0/14","Ain06-11/2/6","Ain07-11/2/7","Din06-11/0/6","Ain08-11/2/8","Aout03-11/3/3","Ain09-11/2/9","Din08-11/0/8", "Din07-11/0/7","Din02-11/0/2",0 )

[MacroLibs]
//Macro Library
D:/users/asc/repo/EibPC/lib/WebOutputHkGen.lib

[InitGA]
// Hk1 VL
"Ain02-11/2/2"			

[Location]
// Longitude and Latitude of Location
11.5
48.21666666666667

[EibPC]
#include D:/Users/asc/repo/EibPC/include/DefHz.inc
#include D:\users\asc\repo\EibPC\include\IDviso.inc
//#include D:/Users/asc/repo/EibPC/include/WebServer.inc
//#include D:/Users/asc/repo/EibPC/include/RemMem.inc
//
// to be moved to include
//
/////////////////////////////////////////
// Basics
////
//////////////////////////////////////
// send Date/ Time Signal
//
///  Date / Time
MinuteTimeCycle	= 10
MinuteDayCycle	= 60
//
// Time
//
if(cycle(MinuteTimeCycle,0)) then{
 	write("sTime-0/0/1",settime())
	} endif
/////////
// Date
//
if(cycle(MinuteDayCycle,0)) then{
 	write("sDate-0/0/4",setdate())
	} endif
//////////
// IsDay ; bIsNight
//
if( change(sun())) then {
	write("bIsDay-0/0/7",sun());
	write("bIsNight-0/0/8", !sun())
	} endif
if(eventread("bIsDay-0/0/7")) then{
	write("bIsDay-0/0/7",sun())
	} endif
if(eventread("bIsNight-0/0/8")) then{
	write("bIsNight-0/0/8",!sun())
	} endif
//
/////////////////////////////
//
// Heating
//
///////////////////////////////////////////////////////
//
UVR_REDUCED = UVR_ABGESENKT
//
///////////////////////////////
//
// Boost /////////////////////
//
BoostHk3 = AUS
//
// automatic Threshold from OG1 requests
//
Boost3Threshold = AUS
//
// Boost manual request for EG Kueche / Esszimmer
//
Boost3EgK	= AUS
//
if( change( Boost3Threshold) or change( Boost3EgK)) then{
	BoostHk3 = AUS;
	if (	Boost3Threshold or
		Boost3EgK) then{
			BoostHk3 = EIN;
			} endif;
		
	if(  BoostHk3) then write( "Aout05-11/3/5",UVR_NORMAL) endif;
	if( !BoostHk3) then write( "Aout05-11/3/5",UVR_REDUCED) endif;
	}endif
//	
///////////////////////////////////////////////////
//			
// Boost EG, Kueche
//
// start Time LT
BOOST_EG_K_START 	= 5u08
//
if(htime(BOOST_EG_K_START,0,0) ) then write("HzEgBoost_EssZ-8/2/39",EIN) endif
// Boost duration, Kueche 2h
//
BOOST_EG_K_DURATION	= 7200000u64

//
if(delayc("HzEgBoost_EssZ-8/2/39",BOOST_EG_K_DURATION,Boost_Eg_K_Remaining)) then write("HzEgBoost_EssZ-8/2/39",AUS) endif
//
if(event("HzEgBoost_EssZ-8/2/39") ) then{
	write("SetLockPosE-3/2/100","HzEgBoost_EssZ-8/2/39");
	write("SetLockPosF-3/2/101","HzEgBoost_EssZ-8/2/39")
	}endif

if( event("HzEgBoost_EssZ-8/2/39")) 	then Boost3EgK = "HzEgBoost_EssZ-8/2/39" endif

//
/////////////////////////////////////////////////////////////////////////////////
// Boost HK3  when request from OG1 above threshold
//
// threshold
rP = 95%
//
if( "OG1MaxStellwertOut-3/3/105" > rP) then Boost3Threshold = EIN endif
if( "OG1MaxStellwertOut-3/3/105" < rP) then Boost3Threshold = AUS endif
//
//////////////////////////////////////////////////////////////////////
//
// WebServer Logic
//
// Button BoostEgK ////////////////////////////////////////////////////
//
if( button(ID_BOOST_EG_K) == 1) then write ("HzEgBoost_EssZ-8/2/39" ,!"HzEgBoost_EssZ-8/2/39") endif;
//
Boost_Eg_K_Remaining 	= 0u64
//
if( "HzEgBoost_EssZ-8/2/39") then webdisplay(ID_BOOST_EG_K,Boost_Eg_K_Remaining/60000u64,WALLHEATING ,DARKRED,GREY) endif;
if( !"HzEgBoost_EssZ-8/2/39" ) then {
		Boost_Eg_K_Remaining = 0u64;
		webdisplay(ID_BOOST_EG_K,Boost_Eg_K_Remaining,WALLHEATING ,INACTIVE,GREY)
		}endif

if( cycle(1,0) and Boost_Eg_K_Remaining != 0u64) then webdisplay(ID_BOOST_EG_K,Boost_Eg_K_Remaining/60000u64,WALLHEATING ,DARKRED,GREY) endif;
//
///////////////////////////////////////////////////////////////////////

[WebServer]

page(P_ID_GEN_1)[$General$,$Heating$]
header(0)
footer(0)
//design $black$

weboutput(ID_WebOutHK1)[SINGLE,NOICON] 
weboutput(ID_WebOutHK3)[SINGLE,NOICON]
weboutput(ID_WebOutHK4)[SINGLE,NOICON]
button(ID_BOOST_EG_K)[WALLHEATING]$Boost Küche$

//////////////////////////////////////////////////////



