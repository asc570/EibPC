
[ETS-ESF]
// ESF file exported from ETS3
D:/users/asc/repo/EibPC/EtsExport/Weisshauptstr.esf

[Macros]
// heating
SpWebOutHkGen($EG FBH$,ID_WebOutHK1,	"Din14-11/0/14","Ain02-11/2/2" ,"Ain03-11/2/3" ,"Din04-11/0/4","Ain04-11/2/4"   ,"Aout04-11/3/4","Ain05-11/2/5" ,"Din010-11/0/10","Din09-11/0/9","Din01-11/0/1",0)
SpWebOutHkGen($RAD$,ID_WebOutHK3,	"Din14-11/0/14","Ain10-11/2/10","Ain11-11/2/11","Din05-11/0/5",0                ,"Aout05-11/3/5","Ain15-11/2/15","Din08-11/0/8"  ,"Din07-11/0/7","Din03-11/0/3","OG1MaxStellwertOut-3/3/105")
SpWebOutHkGen($DACH FBH$,ID_WebOutHK4,	"Din14-11/0/14","Ain06-11/2/6" ,"Ain07-11/2/7" ,"Din06-11/0/6","Ain08-11/2/8"   ,"Aout06-11/3/6","Ain09-11/2/9" ,"Din08-11/0/8"  ,"Din07-11/0/7","Din02-11/0/2",0 )
SpWebButtonMEGFrost( ID_B_OG1_MARISA_MEG_FROST,"SetMEG_Marisa_FrostMode-7/2/24","StMEG_Marisa_FrostMode-7/2/25","GetMEG_Marisa_SollT-7/2/17","GetMEG_Marisa_IstT-7/2/19" )
SpWebButtonMEGFrost( ID_B_OG1_BUERO_MEG_FROST,"SetMEG_Buero_FrostMode-7/2/38","GetMEG_Buero_FrostMode-7/2/39","GetMEG_Buero_SollT-7/2/31","T_OG1_Buero_Merten_MEG-6/2/3" )
SpWebButtonMEGFrost( ID_B_OG1_EMILIA_MEG_FROST,"SetMEG_Emilia_FrostMode-7/2/10","StMEG_Emilia_FrostMode-7/2/11","GetMEG_Emilia_SollT-7/2/3","Aout02-11/3/2")
SpShifterAKH(ID_S_EG_WC_HZ,WALLHEATING,"SetTShiftA-3/2/34","HVAC_A-3/2/40","HVACstA-3/2/52","GetTargetTA-3/2/46","tEgWCElsnerTH_UP-6/1/11","GetPosA-3/2/8","SetLockPosA-3/2/96")
SpShifterAKH( ID_S_EG_WOHNZ_RAD  ,WALLHEATING,"GetSetShiftT_C-3/2/104","HVAC_C-3/2/42","HVACstC-3/2/54","GetTargetTC-3/2/48","tEgWohnzWest-6/1/18","GetPosC-3/2/10","SetLockPosC-3/2/98")
SpShifterAKH( ID_S_EG_ESSZ_RAD,WALLHEATING, "SetTShiftE-3/2/38","HVAC_E-3/2/44","HVACstE-3/2/56","GetTargetTE-3/2/50","Aout01-11/3/1","GetPosE-3/2/12","SetLockPosE-3/2/100")
SpButtonAKH(ID_B_EG_GARDROBE_RAD,WALLHEATING,"HkSwB-3/2/1","GetPosB-3/2/9","SetLockPosB-3/2/97")
SpButtonAKH(ID_B_EG_ESSZ_FBH,FLOORHEATING,"HkSwH-3/2/7","GetPosH-3/2/122","SetLockPosH-3/2/126")
SpButtonAKH(ID_B_EG_WOHNZ_FBH,FLOORHEATING,"HkSwG-3/2/6","GetPosG-3/2/121","SetLockPosG-3/2/125")
SpButtonAKH(ID_B_DACH_BAD_FBH,FLOORHEATING,"SwHkF-3/4/160","StSwHkF-3/4/166","LockPosF-3/4/118")
ShifterEMO(ID_S_EG_EMO_TREPPE,WALLHEATING,"HkProp2Soll-3/3/14","HkProp2Ist-3/3/15","HkProp2Lock-3/3/138")
SpWebButtonBlockEgBoost(ID_B_BOOST_EG_Kueche ,bRemBoostEG_K_Block,"fHzBlockEgBoost-8/2/52")
SpShifterUVRmode(ID_S_HK1_MODE,"Ain05-11/2/5","Aout04-11/3/4")
SpShifterUVRmode(ID_S_HK4_MODE,"Ain09-11/2/9","Aout06-11/3/6")
SpShifterUVRost(ID_S_HK4_DACH_EAST,"SwFBH4DachOst-3/4/183","T_Dach_Ost_Zas_Schreibtisch-6/3/11","Aout03-11/3/3","SwHkE-3/4/159","StSwHkE-3/4/165","shiftT_FBH4DachOst-3/4/181","shiftT_FBH4DachGen-3/4/182",bFBHdachOst,corRoomEast,corGenHk4)
SpButtonUVRon(ID_B_UVR_ON,"Dout01-11/1/1" ,"Din14-11/0/14","Ain16-11/2/16")

[MacroLibs]
D:/users/asc/repo/EibPC/lib/WebOutputHkGen.lib

[InitGA]
// outsideTemp
"Ain16-11/2/16"
// heating
"Ain02-11/2/2"	
// for SpWebButtonMEGFrost( ID_B_OG1_MARISA_MEG_FROST,..)
"StMEG_Marisa_FrostMode-7/2/25"
"GetMEG_Marisa_SollT-7/2/17"
"GetMEG_Marisa_IstT-7/2/19" 
// for SpWebButtonMEGFrost( ID_B_OG1_BUERO_MEG_FROST,	
"GetMEG_Buero_FrostMode-7/2/39"	
"GetMEG_Buero_SollT-7/2/31"
"T_OG1_Buero_Merten_MEG-6/2/3"
// for SpWebButtonMEGFrost( ID_B_OG1_EMILIA_MEG_FROST,..)
"StMEG_Emilia_FrostMode-7/2/11"
"GetMEG_Emilia_SollT-7/2/3"
"Aout02-11/3/2"
// for SpShifterAKH(ID_S_EG_WC,...)
"HVACstA-3/2/52"
"GetTargetTA-3/2/46"
"tEgWCElsnerTH_UP-6/1/11"
"GetPosA-3/2/8"
"SetLockPosA-3/2/96"
// for SpShifterAKH( ID_S_EG_WOHNZ_RAD  ,...)
"HVACstC-3/2/54"
"GetTargetTC-3/2/48"
"tEgWohnzWest-6/1/18"
"GetPosC-3/2/10"
"SetLockPosC-3/2/98"
// for SpShifterAKH( ID_S_EG_ESSZ_RAD ...)
"HVACstE-3/2/56"
"GetTargetTE-3/2/50"
"Aout01-11/3/1"
"GetPosE-3/2/12"
"SetLockPosE-3/2/100"
// for SpButtonAKH(ID_B_EG_GARDROBE_RAD,...)
"GetPosB-3/2/9"
"SetLockPosB-3/2/97"
// for SpButtonAKH(ID_B_EG_ESSZ_FBH,..)
"GetPosH-3/2/122"
"SetLockPosH-3/2/126"
// for SpButtonAKH(ID_B_EG_WOHNZ_FBH,...)
"GetPosG-3/2/121"
"SetLockPosG-3/2/125"
//for ShifterEMO(ID_S_EG_EMO_TREPPE..)
"HkProp2Ist-3/3/15"
"HkProp2Lock-3/3/138"
//for SpShifterUVRost()
"T_Dach_Ost_Zas_Schreibtisch-6/3/11"
"T_Dach_Ost_F50west-6/3/10"
"StSwHkE-3/4/165"


[Location]
// Longitude and Latitude of Location
11.5
48.21666666666667

[EibPC]
#include D:/Users/asc/repo/EibPC/include/DefHz.inc
#include D:\users\asc\repo\EibPC\include\IDviso.inc
//#include D:/Users/asc/repo/EibPC/include/WebServer.inc
//#include D:/Users/asc/repo/EibPC/include/RemMem.inc
//
/////////////////////////////////////////
// Basics
////
//////////////////////////////////////
// send Date/ Time Signal
//
///  Date / Time
MinuteTimeCycle	= 10
MinuteDayCycle	= 60
//
// Time
//
if(cycle(MinuteTimeCycle,0)) then{
 	write("sTime-0/0/1",settime())
	} endif
/////////
// Date
//
if(cycle(MinuteDayCycle,0)) then{
 	write("sDate-0/0/4",setdate())
	} endif
//////////
// IsDay ; bIsNight
//
if( change(sun())) then {
	write("bIsDay-0/0/7",sun());
	write("bIsNight-0/0/8", !sun())
	} endif
if(eventread("bIsDay-0/0/7")) then{
	write("bIsDay-0/0/7",sun())
	} endif
if(eventread("bIsNight-0/0/8")) then{
	write("bIsNight-0/0/8",!sun())
	} endif
//
/////////////////////////////
//
// Heating
//
///////////////////////////////////////////////////////
//
UVR_REDUCED = UVR_ABGESENKT
//
///////////////////////////////
//
// Boost /////////////////////
//
BoostHk3 = AUS
//
// automatic Threshold from OG1 requests
//
Boost3Threshold = AUS
//
// Boost manual request for EG Kueche / Esszimmer
//
Boost3EgK	= AUS
//
if( change( Boost3Threshold) or change( Boost3EgK)) then{
	BoostHk3 = AUS;
	if (	Boost3Threshold or Boost3EgK) then{ BoostHk3 = EIN} endif;
		
	if(  BoostHk3) then write( "Aout05-11/3/5",UVR_NORMAL) endif;
	if( !BoostHk3) then write( "Aout05-11/3/5",UVR_REDUCED) endif;
	}endif
//	
///////////////////////////////////////////////////
//			
// Boost EG, Kueche
bRemBoostEG_K_Block	= AUS
// 
if systemstart() then readflashvar(bRemBoostEG_K_Block) endif
if change(bRemBoostEG_K_Block) then writeflashvar(bRemBoostEG_K_Block) endif
if event("fHzBlockEgBoost-8/2/52") then bRemBoostEG_K_Block = "fHzBlockEgBoost-8/2/52" endif
//
// start Time LT
BOOST_EG_K_START_H 	= 6u08
BOOST_EG_K_START_M 	= 30u08
//
if(htime(BOOST_EG_K_START_H,BOOST_EG_K_START_M ,0) and !bRemBoostEG_K_Block) then write("HzEgBoost_EssZ-8/2/39",EIN) endif
// Boost duration, Kueche 2h
//
BOOST_EG_K_DURATION	= 3600000u64

//
if(delayc("HzEgBoost_EssZ-8/2/39",BOOST_EG_K_DURATION,Boost_Eg_K_Remaining)) then write("HzEgBoost_EssZ-8/2/39",AUS) endif
//
if(event("HzEgBoost_EssZ-8/2/39") ) then{
	write("SetLockPosE-3/2/100","HzEgBoost_EssZ-8/2/39");
	write("SetLockPosF-3/2/101","HzEgBoost_EssZ-8/2/39")
	}endif

if( event("HzEgBoost_EssZ-8/2/39")) 	then Boost3EgK = "HzEgBoost_EssZ-8/2/39" endif

//
/////////////////////////////////////////////////////////////////////////////////
// Boost HK3  when request from OG1 above threshold
//
// threshold
rP = 95%
upperLboostOG1	= 95%
lowerLboostOG1	= 90%
//
if( hysteresis("OG1MaxStellwertOut-3/3/105",lowerLboostOG1,upperLboostOG1)) 	then Boost3Threshold = EIN \\
										else Boost3Threshold = AUS endif
//
//////////////////////////////////////////////////////////////////////
//
tempStep 	= 0.5f16
corRoomEast	= 0f16
if systemstart() then readflashvar(corRoomEast) endif
if change(corRoomEast) then writeflashvar(corRoomEast) endif
if event("shiftT_FBH4DachOst-3/4/181") then{
	if "shiftT_FBH4DachOst-3/4/181" then corRoomEast = corRoomEast + tempStep endif;
	if !"shiftT_FBH4DachOst-3/4/181" then corRoomEast = corRoomEast - tempStep endif
	}endif;

corGenHk4	= 0f16
if systemstart() then readflashvar(corGenHk4) endif
if change(corGenHk4) then writeflashvar(corGenHk4) endif
if event("shiftT_FBH4DachGen-3/4/182") then{
	if "shiftT_FBH4DachGen-3/4/182" then corGenHk4 = corGenHk4 + tempStep endif;
	if !"shiftT_FBH4DachGen-3/4/182" then corGenHk4 = corGenHk4 - tempStep endif
	}endif

bFBHdachOst	= 1b01
if systemstart() then readflashvar(bFBHdachOst) endif
if change(bFBHdachOst) then writeflashvar(bFBHdachOst) endif
if event("SwFBH4DachOst-3/4/183") then bFBHdachOst = "SwFBH4DachOst-3/4/183" endif

// 


//////////////////////////////////////////////////////////////////////
//
// WebServer Logic
//
// Button BoostEgK ////////////////////////////////////////////////////
//
if( button(ID_B_BOOST_EG_K) != 0) then write ("HzEgBoost_EssZ-8/2/39" ,!"HzEgBoost_EssZ-8/2/39") endif;
//
Boost_Eg_K_Remaining 	= 0u64
// 
if( "HzEgBoost_EssZ-8/2/39") then{
 	webdisplay(ID_B_BOOST_EG_K,stringformat(Boost_Eg_K_Remaining/60000u64,0,3,2,2)+ $ minutes remaining$,WALLHEATING ,DARKRED,GREY) 
	}endif;
if( !"HzEgBoost_EssZ-8/2/39" ) then {
	Boost_Eg_K_Remaining = 0u64;
	webdisplay(ID_B_BOOST_EG_K,$$,WALLHEATING ,INACTIVE,GREY)
	}endif

if( cycle(1,0) and Boost_Eg_K_Remaining != 0u64) then webdisplay(ID_B_BOOST_EG_K,Boost_Eg_K_Remaining/60000u64,WALLHEATING ,DARKRED,GREY) endif;
//
///////////////////////////////////////////////////////////////////////

[WebServer]

page(P_ID_GEN_1)[$General$,$Heating$]
header(0)
footer(0)
//design $black$
button(ID_B_UVR_ON) [UPDATE] $Heating ON/OFF$
weboutput(ID_WebOutHK4)[SINGLE,NOICON]
weboutput(ID_WebOutHK3)[SINGLE,NOICON]
weboutput(ID_WebOutHK1)[SINGLE,NOICON] 
line $Dach$
shifter(ID_S_HK4_DACH_EAST)[UP,FLOORHEATING,DOWN]  $FBH Dach Raum Ost$	
line $EG$
shifter(ID_S_EG_ESSZ_RAD )[UP,WALLHEATING,DOWN]	$EG Esszimmer$
button(ID_B_BOOST_EG_K)[WALLHEATING]	$EG Boost Kueche$	button(ID_B_BOOST_EG_Kueche)[SWITCH] $EG Auto Boost$ 
shifter(ID_S_EG_WOHNZ_RAD )[UP,WALLHEATING,DOWN]$EG Wohnzimmer$

//////////////////////////////////////////////////////////////
page(P_ID_ADMIN_HEATING)[$Admin$,$Heating$]
line$Dach$
weboutput(ID_WebOutHK4)[SINGLE,NOICON]
shifter(ID_S_HK4_MODE)[CLOCK,WEATHER,NIGHT,UPDATE] $FBH Dach Mode$
button(ID_B_DACH_BAD_FBH)[FLOORHEATING] $Dach Bad FBH$
line $OG1$
weboutput(ID_WebOutHK3)[SINGLE,NOICON]	
button( ID_B_OG1_MARISA_MEG_FROST)[ICE]	$OG1 Marisa Frost$	button( ID_B_OG1_EMILIA_MEG_FROST)[ICE]$OG1 Emilia Frost$
button(ID_B_OG1_BUERO_MEG_FROST )[ICE]	$OG1 Buero Frost$ 
line $EG$
weboutput(ID_WebOutHK1)[SINGLE,NOICON] 	
shifter(ID_S_HK1_MODE)[CLOCK,WEATHER,NIGHT,UPDATE] $FBH EG Mode$
button(ID_B_EG_WOHNZ_FBH )[FLOORHEATING] $EG Wohnz. FBH$	button(ID_B_EG_GARDROBE_RAD )[WALLHEATING] $EG Gardrobe$ 	
shifter(ID_S_EG_WC_HZ)[UP,WALLHEATING,DOWN] 	$EG Gäste-WC$
shifter(ID_S_EG_EMO_TREPPE)[UP,WALLHEATING,DOWN]$EG Treppenhaus$
		


//////////////////////////////////////////////////////



