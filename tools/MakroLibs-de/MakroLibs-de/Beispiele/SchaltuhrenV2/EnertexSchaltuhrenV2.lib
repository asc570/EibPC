//----------------------------------------------------------------------
//  Makros für den Enertex EiBPC
//
//  Schaltuhren
//
//  Version              : v3.100
//
//  License              : Verwendung nur für die Nutzung mit dem 
//                         Enertex EibPC gestattet 
//
//  (C) Copyright        : Enertex Bayern GmbH. All rights reserved.
//
//  Author               : Enertex Bayern GmbH
// 
//  Dat.Rev.             : $Date: 2016/07/22 09:47:58 $
//  Version              : $Revision: 1.11 $
//----------------------------------------------------------------------


// WochenSchaltuhr
// @date	11.01.2012
// @version	1 
// @author	Enertex Bayern GmbH
:begin WochenSchaltuhr(TAG,Stunde,Minute,GASchalten,Wert)
:info $Gibt jede Woche zur festgelegten Zeit einen Wert auf die Gruppenadresse aus$\\
$Wochentag des Schaltzeitpunkts$ $Stunde des Schaltzeitpunkts$ $Minute des Schaltzeitpunkts$\\
$Gruppenadresse des Schaltaktors$\\
$Wert, der an die Gruppenadresse des Schaltaktors gesendet werden soll. Muss zum Datentyp des Schaltaktors passen$
:shortinfo $Jede Woche zu einem bestimmten Zeitpunkt eine Gruppenadresse schalten$
if wtime(Stunde,Minute,0u08,TAG) then write(GASchalten,Wert) endif
:end

// WochenSchaltuhr GA
// @date	11.01.2012
// @version	1 
// @author	Enertex Bayern GmbH
:begin WochenSchaltuhr_2GA(TAG,Stunde,Minute,GA1Schalten, Wert1,GA2Schalten,Wert2)
:info $Gibt jede Woche zur festgelegten Zeit je einen Wert auf die Gruppenadressen aus$\\
$Wochentag des Schaltzeitpunkts$ $Stunde des Schaltzeitpunkts$ $Minute des Schaltzeitpunkts$\\
$Gruppenadresse des Schaltaktors 1$\\
$Wert, der an die Gruppenadresse des Schaltaktors 1 gesendet werden soll. Muss zum Datentyp des Schaltaktors 1 passen$\\
$Gruppenadresse des Schaltaktors 2$\\
$Wert, der an die Gruppenadresse des Schaltaktors 2 gesendet werden soll. Muss zum Datentyp des Schaltaktors 2 passen
:shortinfo $Jede Woche zu einem bestimmten Zeitpunkt zwei Gruppenadresse schalten$
if wtime(Stunde,Minute,0u08,TAG) then write(GA1Schalten,Wert1); write(GA2Schalten,Wert2) endif
:end

// schaltet täglich
// @date	11.01.2012
// @version	1 
// @author	Enertex Bayern GmbH
:begin TagesSchaltuhr(Stunde,Minute,GASchalten,Wert)
:info $Gibt jeden Tag zur festgelegten Zeit einen Wert auf die Gruppenadresse aus$\\
$Stunde des Schaltzeitpunkts$ $Minute des Schaltzeitpunkts$\\
$Gruppenadresse des Schaltaktors$\\
$Wert, der an die Gruppenadresse des Schaltaktors gesendet werden soll. Muss zum Datentyp des Schaltaktors passen$
:shortinfo $Jeden Tag zu einem bestimmten Zeitpunkt eine Gruppenadresse schalten$
if htime(Stunde,Minute,0u08) then write(GASchalten,Wert) endif
:end

// TagesSchaltuhr GA
// @date	11.01.2012
// @version	1 
// @author	Enertex Bayern GmbH
:begin TagesSchaltuhr_2GA(Stunde,Minute,GA1Schalten,Wert1,GA2Schalten,Wert2)
:info $Gibt jeden Tag zur festgelegten Zeit je einen Wert auf die Gruppenadressen aus$\\
$Stunde des Schaltzeitpunkts$ $Minute des Schaltzeitpunkts$\\
$Gruppenadresse des Schaltaktors 1$\\
$Wert, der an die Gruppenadresse des Schaltaktors 1 gesendet werden soll. Muss zum Datentyp des Schaltaktors 1 passen$\\
$Gruppenadresse des Schaltaktors 2$\\
$Wert, der an die Gruppenadresse des Schaltaktors 2 gesendet werden soll. Muss zum Datentyp des Schaltaktors 2 passen
:shortinfo $Jeden Tag zu einem bestimmten Zeitpunkt zwei Gruppenadresse schalten$
if htime(Stunde,Minute,0u08) then write(GA1Schalten,Wert1); write(GA2Schalten,Wert2) endif
:end


// Bei Sonnenaufgang
// @date	4.08.11 / 11.01.2012
// @version	1 
// @author	Enertex Bayern GmbH
:begin Bei_Sonnenaufgang(AktorGA, AktorWert, Zeit)
:info $Gibt bei Sonnenaufgang einen Wert auf eine Gruppenadresse aus. Das Programm muss dazu die geographische Länge und Breite des betreffenden Ortes kennen. Dies kann man unter Optionen --> Koordinaten für Sonnenstandberechnung einstellen$\\
$Gruppenadresse des Aktors$\\
$Wert des Aktors(muss zum Datentyp des Aktors passen)$\\
$Zeit in ms, die vergehen soll, bis der Wert gesendet wird, nach dem Sonnenaufgang$
:shortinfo $Der Aktor wird bei Sonnenaufgang geschalten$
//bei systemstart schaltet er dann schon den Aktor, aber ohne Verzoegerung
if systemstart() and sun() then write(AktorGA, AktorWert) endif
if after(sun(), Zeit^u64 + 1u64) and sun()  then write(AktorGA, AktorWert) endif

:end

// Bei Sonnenuntergang
// @date	4.08.11 / 11.01.2012
// @version	1 
// @author	Enertex Bayern GmbH
:begin Bei_Sonnenuntergang(AktorGA, AktorWert, Zeit)
:info $Gibt bei Sonnenuntergang einen Wert auf eine Gruppenadresse aus. Das Programm muss dazu die geographische Länge und Breite des betreffenden Ortes kennen. Dies kann man unter Optionen --> Koordinaten für Sonnenstandberechnung einstellen$\\
$Gruppenadresse des Aktors$\\
$Wert des Aktors(muss zum Datentyp des Aktors passen)$\\
$Zeit in ms, die vergehen soll, bis der Wert gesendet wird, nach dem Sonnenaufgang$
:shortinfo $Der Aktor wird bei Sonnenuntergang geschalten$
//bei systemstart schaltet er dann schon den Aktor, aber ohne Verzoegerung
if systemstart() and !sun() then write(AktorGA, AktorWert) endif
if after(!sun(), Zeit^u64 + 1u64) and !sun() then write(AktorGA, AktorWert) endif

:end

// Bei Sonnenaufgang gedeckelt
// @date	4.08.11 / 11.01.2012
// @version	1 
// @author	Enertex Bayern GmbH
:begin Bei_Sonnenaufgang_Gedeckelt(Name, AktorGA, AktorWert, Zeit, Stunde, Minute, Sekunde)
:info $Gibt bei Sonnenaufgang oder spätestens zu einem bestimmten Zeitpunkt einen Wert auf eine Gruppenadresse aus. Das Programm muss dazu die geographische Länge und Breite des betreffenden Ortes kennen. Dies kann man unter Optionen --> Koordinaten für Sonnenstandberechnung einstellen$\\
$Name des Makros - muss eindeutig sein$\\
$Gruppenadresse des Aktors$\\
$Wert des Aktors(muss zum Datentyp des Aktors passen)$\\
$Zeit in ms, die vergehen soll, bis der Wert gesendet wird, nach dem Sonnenaufgang$\\
$Die Stunde zu dem der Wert spätestens gesendet werden soll$\\
$Die Minute zu dem der Wert spätestens gesendet werden soll$\\
$Die Sekunde zu dem der Wert spätestens gesendet werden soll$
:shortinfo $Der Aktor wird bei Sonnenaufgang geschalten oder spätestens zu einem bestimmten Zeitpunkt$ 
//bei systemstart schaltet er dann schon den Aktor, aber ohne Verzoegerung
//if systemstart() and sun() then write(AktorGA, AktorWert) endif
//Das der write-Befehl nicht 2mal gesendet wird
Name^Sonnenaufgang = AUS
if after(sun(), Zeit^u64 + 1u64) and sun() then {
	Name^Sonnenaufgang=EIN
} endif

if Name^Sonnenaufgang or chtime(Stunde,Minute,Sekunde) then  {
	write(AktorGA, AktorWert)
} endif
if !sun() then {
	Name^Sonnenaufgang=AUS
} endif 

:end

// Bei Sonnenuntergang gedeckelt
// @date	4.08.11 / 11.01.2012
// @version	1 
// @author	Enertex Bayern GmbH
:begin Bei_Sonnenuntergang_Gedeckelt(Name, AktorGA, AktorWert, Zeit, Stunde, Minute, Sekunde)
:info $Gibt bei Sonnenuntergang oder spätestens zu einem bestimmten Zeitpunkt einen Wert auf eine Gruppenadresse aus. Das Programm muss dazu die geographische Länge und Breite des betreffenden Ortes kennen. Dies kann man unter Optionen --> Koordinaten für Sonnenstandberechnung einstellen$\\
$Name des Makros - muss eindeutig sein$\\
$Gruppenadresse des Aktors$\\
$Wert des Aktors(muss zum Datentyp des Aktors passen)$\\
$Zeit in ms, die vergehen soll, bis der Wert gesendet wird, nach dem Sonnenuntergang$\\
$Die Stunde zu dem der Wert spätestens gesendet werden soll$\\
$Die Minute zu dem der Wert spätestens gesendet werden soll$\\
$Die Sekunde zu dem der Wert spätestens gesendet werden soll$
:shortinfo $Der Aktor wird bei Sonnenuntergang geschalten oder spätestens zu einem bestimmten Zeitpunkt$
//bei systemstart schaltet er dann schon den Aktor, aber ohne Verzoegerung
//if systemstart() and !sun() then write(AktorGA, AktorWert) endif
//Das der write-Befehl nicht 2mal gesendet wird
Name^Sonnenuntergang = AUS
if after(!sun(), Zeit^u64 + 1u64) and !sun() then {
	Name^Sonnenuntergang=EIN
} endif

if Name^Sonnenuntergang or chtime(Stunde,Minute,Sekunde) then  {
	write(AktorGA, AktorWert)
} endif
if sun() then {
	Name^Sonnenuntergang=AUS
} endif 

:end

// Bei Sonnenaufgang gedeckelt mit Freigabe
// @date	4.08.11 / 11.01.2012
// @version	1 
// @author	Enertex Bayern GmbH
:begin Bei_Sonnenaufgang_Gedeckelt_mitFreigabe(Name, Freigabe, AktorGA, AktorWert, Zeit, Stunde, Minute, Sekunde)
:info $Gibt bei Sonnenaufgang oder spätestens zu einem bestimmten Zeitpunkt einen Wert auf eine Gruppenadresse aus. Das Programm muss dazu die geographische Länge und Breite des betreffenden Ortes kennen. Dies kann man unter Optionen --> Koordinaten für Sonnenstandberechnung einstellen. Mit Freigabeobjekt.$\\
$Name des Makros - muss eindeutig sein$\\
$Gruppenadresse eines FreigabeObjektes oder Variable$\\
$Gruppenadresse des Aktors$\\
$Wert des Aktors(muss zum Datentyp des Aktors passen)$\\
$Zeit in ms, die vergehen soll, bis der Wert gesendet wird, nach dem Sonnenaufgang$\\
$Die Stunde zu dem der Wert spätestens gesendet werden soll$\\
$Die Minute zu dem der Wert spätestens gesendet werden soll$\\
$Die Sekunde zu dem der Wert spätestens gesendet werden soll$
:shortinfo $Der Aktor wird bei Sonnenaufgang geschalten oder spätestens zu einem bestimmten Zeitpunkt$ 
//Das der write-Befehl nicht 2mal gesendet wird
Name^Sonnenaufgang = AUS
if after(sun(), Zeit^u64 + 1u64) and sun() then  {
    Name^Sonnenaufgang=EIN
} endif

if Freigabe and (Name^Sonnenaufgang or chtime(Stunde,Minute,Sekunde)) then  {
	write(AktorGA, AktorWert)
} endif
if !sun() then {
	Name^Sonnenaufgang=AUS
} endif 

:end

// Bei Sonnenuntergang gedeckelt mit Freigabe
// @date	4.08.11 / 11.01.2012
// @version	1 
// @author	Enertex Bayern GmbH
:begin Bei_Sonnenuntergang_Gedeckelt_mitFreigabe(Name, Freigabe, AktorGA, AktorWert, Zeit, Stunde, Minute, Sekunde)
:info $Gibt bei Sonnenuntergang oder spätestens zu einem bestimmten Zeitpunkt einen Wert auf eine Gruppenadresse aus. Das Programm muss dazu die geographische Länge und Breite des betreffenden Ortes kennen. Dies kann man unter Optionen --> Koordinaten für Sonnenstandberechnung einstellen. Mit Freigabeobjekt.$\\
$Name des Makros - muss eindeutig sein$\\
$Gruppenadresse des Aktors$\\
$Gruppenadresse eines FreigabeObjektes oder Variable$\\
$Wert des Aktors(muss zum Datentyp des Aktors passen)$\\
$Zeit in ms, die vergehen soll, bis der Wert gesendet wird, nach dem Sonnenuntergang$\\
$Die Stunde zu dem der Wert spätestens gesendet werden soll$\\
$Die Minute zu dem der Wert spätestens gesendet werden soll$\\
$Die Sekunde zu dem der Wert spätestens gesendet werden soll$
:shortinfo $Der Aktor wird bei Sonnenuntergang geschalten oder spätestens zu einem bestimmten Zeitpunkt$
//Das der write-Befehl nicht 2mal gesendet wird
Name^Sonnenuntergang = AUS
if after(!sun(), Zeit^u64 + 1u64) and !sun() then  {
    Name^Sonnenuntergang=EIN
} endif

if Freigabe and (Name^Sonnenuntergang or chtime(Stunde,Minute,Sekunde)) then  {
	write(AktorGA, AktorWert)
} endif
if sun() then {
	Name^Sonnenuntergang=AUS
} endif 

:end


// Schaltuhr Mitternacht
// @date	4.08.11 / 11.01.2012 / 03.12.2014
// @version	3
// @author	Enertex Bayern GmbH (S.M.)
:begin SchaltuhrMitternacht(Name, AktorGA, StundeEin, MinuteEin, StundeAus, MinuteAus)
:info $Man kann mit dieser Schaltuhr einen Aktor zu bestimmten Zeiten ein und ausschalten, der Aktor wird auch bei einem Systemstart nach Mitternacht richtig geschalten.$\\
	$Name zweges Eindeutigkeit$\\
	$Gruppenadresse des Aktors$\\
	$Stunde zu der der Aktor eingeschaltet wird$\\
	$Minute zu der der Aktor eingeschaltet wird$\\
	$Stunde zu der der Aktor ausgeschaltet wird$\\
	$Minute zu der der Aktor ausgeschaltet wird$
:shortinfo $Schaltuhr mit der man einen Aktor ein und ausschalten kann, auch über Mitternacht$
Laufzeit^Name = AUS
Mitternacht^Name = AUS
//nicht über Mitternacht
if (StundeEin < StundeAus) or ((StundeEin == StundeAus) and (MinuteEin < MinuteAus)) then Mitternacht^Name=AUS endif
//über Mitternacht
if (StundeEin > StundeAus) or ((StundeEin == StundeAus) and (MinuteEin > MinuteAus)) then Mitternacht^Name=EIN endif

if !Mitternacht^Name and (chtime(StundeEin,MinuteEin,0)  and !chtime(StundeAus,MinuteAus,0)) then Laufzeit^Name=EIN else Laufzeit^Name=AUS endif
if Mitternacht^Name and (chtime(StundeEin,MinuteEin,0)  or !chtime(StundeAus,MinuteAus,0)) then Laufzeit^Name=EIN else Laufzeit^Name=AUS endif

if Laufzeit^Name then write(AktorGA, EIN) else write(AktorGA,AUS) endif
:end

// Bei Sonnenaufgang fruehestens
// @date	4.08.11 / 11.01.2012
// @version	1 
// @author	Enertex Bayern GmbH
:begin SonnenaufgangFruehestens(AktorGA, WertGA,Stunde,Minute)
:info $Der Aktor wird bei Sonnenaufgang geschaltet, wenn die angegebene Zeit schon verstrichen ist. Ist der Sonnenaufgang vor der angegeben Zeit, wird der Aktor zu dieser Zeit geschaltet.$\\
	$Gruppenadresse des Aktors$\\
	$Wert des Aktors(muss zum Datentyp des Aktors passen)$\\
	$Stunde, vor der noch nicht geschaltet werden soll$\\
	$Minute, vor der noch nicht geschaltet werden soll$
:shortinfo $Aktor bei Sonnenaufgang schalten, aber nicht vor einer bestimmten Zeit$

if sun() and chtime(Stunde,Minute,0) then write(AktorGA, WertGA) endif

:end 
